Resources
| where type =~ 'microsoft.compute/virtualmachines'
| where isnull(tags['AppPortID']) or tags['AppPortID'] == ''
| project vmId = id, vmName = name, vmRG = resourceGroup, location
| join kind=leftouter (
    Resources
    | where type =~ 'microsoft.compute/disks'
    | where name has 'IdentityDisk'
    | project diskName = name, diskRG = resourceGroup, diskVmId = tostring(properties.ownerId)
) on $left.vmId == $right.diskVmId
| join kind=leftouter (
    Resources
    | where type =~ 'microsoft.network/networkinterfaces'
    | project nicId = id, nicName = name, nicRG = resourceGroup, nicVmId = tostring(properties.virtualMachine.id), 
              publicIpId = tostring(properties.ipConfigurations[0].properties.publicIPAddress.id),
              nsgId = tostring(properties.networkSecurityGroup.id)
) on $left.vmId == $right.nicVmId
| join kind=leftouter (
    Resources
    | where type =~ 'microsoft.network/publicipaddresses'
    | project publicIpId = id, publicIp = properties.ipAddress
) on $left.publicIpId == $right.publicIpId
| join kind=leftouter (
    Resources
    | where type =~ 'microsoft.network/networksecuritygroups'
    | project nsgId = id, nsgName = name, nsgRG = resourceGroup
) on $left.nsgId == $right.nsgId
| project vmName, vmRG, location, diskName, diskRG, nicName, nicRG, publicIp, nsgName, nsgRG
